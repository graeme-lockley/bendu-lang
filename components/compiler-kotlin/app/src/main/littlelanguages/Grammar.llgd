uses "./Scanner.llld";

Program
    : {Statement [";"]}
    ;

Statement
    : "let" LowerID "=" Expression
    | "print" "(" [Expression {"," Expression}] ")"
    | "println" "(" [Expression {"," Expression}] ")"
    | Expression
    ;

Expression
    : "if" ["|"] Expression "->" Expression {"|" Expression "->" Expression} ["|" Expression]
    | "while" Expression "->" Expression
    | OrExpression
    ;

OrExpression
    : Or
    ;

Or
    : And {"||" And}
    ;

And
    : Equality {"&&" Equality}
    ;

Equality
    : Additive [RelOp Additive]
    ;

RelOp
    : "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

Additive
    : Multiplicative {["+" | "-"] Multiplicative}
    ;

Multiplicative
    : Power {["*" | "/" | "%"] Power}
    ;

Power
    : Factor {"**" Factor}
    ;

Factor
    : "(" [Expression] ")"
    | LowerID
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "!" Factor
    ;
