uses "./Scanner.llld";

Program
    : {Expression [";"]}
    ;

Expression
    : "let" LetDeclaration {"and" LetDeclaration}
    | "print" "(" [Expression {"," Expression}] ")"
    | "println" "(" [Expression {"," Expression}] ")"
    | "abort" "(" [Expression {"," Expression}] ")"
    | "if" ["|"] Expression "->" Expression {"|" Expression ["->" Expression]}
    | "while" Expression "->" Expression
    | OrExpression
    ;

LetDeclaration
    : LowerID ["!"] [TypeParameters] [FunctionParameters] [TypeQualifier] "=" Expression
    ;

OrExpression
    : Or
    ;

Or
    : AndE {"||" AndE}
    ;

AndE
    : Equality {"&&" Equality}
    ;

Equality
    : Additive [RelOp Additive]
    ;

RelOp
    : "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

Additive
    : Multiplicative {("+" | "-") Multiplicative}
    ;

Multiplicative
    : Power {("*" | "/" | "%") Power}
    ;

Power
    : TypedExpression {"**" TypedExpression}
    ;

TypedExpression
    : Application [":" TypeTerm]
    ;

Application
    : Assignment {"(" [Expression {"," Expression}] ")"}
    ;

Assignment
    : Factor [":=" Expression]
    ;

Factor
    : "(" [Expression {"," Expression}] ")"
    | LowerID
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "!" Factor
    | "@" Factor
    | "fn" [TypeParameters] FunctionParameters [TypeQualifier] ["="] Expression
    | "{" {Expression [";"]} "}"
    | "[" [Expression {"," Expression}] "]"
    ;

FunctionParameters
    : "(" [FunctionParameter {"," FunctionParameter}] ")"
    ;

FunctionParameter
    : LowerID ["!"] [TypeQualifier]
    | "_" [TypeQualifier]
    | "(" FunctionParameter {"," FunctionParameter} ")" [TypeQualifier]
    ;

TypeParameters
    : "[" LowerID {"," LowerID} "]"
    ;

TypeQualifier
    : ":" TypeTerm
    ;

TypeTerm
    : TypeFactor {"*" TypeFactor}
    ;

TypeFactor
    : "(" [TypeTerm {"," TypeTerm}] ")" ["->" TypeTerm]
    | UpperID ["[" TypeTerm {"," TypeTerm} "]"]
    | LowerID
    ;
