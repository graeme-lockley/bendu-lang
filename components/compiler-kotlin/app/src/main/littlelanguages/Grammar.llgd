uses "./Scanner.llld";

Program
    : {Expression [";"]}
    ;

Expression
    : "let" LowerID ["(" [LowerID {"," LowerID}] ")"] "=" Expression
    | "print" "(" [Expression {"," Expression}] ")"
    | "println" "(" [Expression {"," Expression}] ")"
    | "abort" "(" [Expression {"," Expression}] ")"
    | "if" ["|"] Expression "->" Expression {"|" Expression ["->" Expression]}
    | "while" Expression "->" Expression
    | OrExpression
    ;

OrExpression
    : Or
    ;

Or
    : And {"||" And}
    ;

And
    : Equality {"&&" Equality}
    ;

Equality
    : Additive [RelOp Additive]
    ;

RelOp
    : "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

Additive
    : Multiplicative {("+" | "-") Multiplicative}
    ;

Multiplicative
    : Power {("*" | "/" | "%") Power}
    ;

Power
    : Factor {"**" Factor}
    ;

Factor
    : "(" [Expression] ")"
    | LowerID ["(" [Expression {"," Expression}] ")"]
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "!" Factor
    | "@" Factor
    ;
