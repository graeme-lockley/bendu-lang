uses "./Scanner.llld";

Program
    : {Expression [";"]}
    ;

Expression
    : StatementExpression {"," StatementExpression}
    ;

StatementExpression
    : "let" LetDeclaration {"and" LetDeclaration}
    | "print" "(" [StatementExpression {"," StatementExpression}] ")"
    | "println" "(" [StatementExpression {"," StatementExpression}] ")"
    | "abort" "(" [StatementExpression {"," StatementExpression}] ")"
    | "if" ["|"] StatementExpression "->" Expression {"|" StatementExpression ["->" Expression]}
    | "while" StatementExpression "->" Expression
    | OrExpression
    ;

LetDeclaration
    : LowerID ["!"] [TypeParameters] [FunctionParameters] [TypeQualifier] "=" StatementExpression
    ;

OrExpression
    : Or
    ;

Or
    : AndE {"||" AndE}
    ;

AndE
    : Equality {"&&" Equality}
    ;

Equality
    : Additive [RelOp Additive]
    ;

RelOp
    : "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

Additive
    : Multiplicative {("+" | "-") Multiplicative}
    ;

Multiplicative
    : Power {("*" | "/" | "%") Power}
    ;

Power
    : TypedExpression {"**" TypedExpression}
    ;

TypedExpression
    : Application [":" TypeTerm]
    ;

Application
    : Assignment {"(" [StatementExpression {"," StatementExpression}] ")"}
    ;

Assignment
    : Factor [":=" Expression]
    ;

Factor
    : "(" [Expression] ")"
    | LowerID
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "!" Factor
    | "@" Factor
    | "fn" [TypeParameters] FunctionParameters [TypeQualifier] ["="] StatementExpression
    | "{" {Expression [";"]} "}"
    ;

FunctionParameters
    : "(" [FunctionParameter {"," FunctionParameter}] ")"
    ;

FunctionParameter
    : LowerID ["!"] [TypeQualifier]
    ;

TypeParameters
    : "[" LowerID {"," LowerID} "]"
    ;

TypeQualifier
    : ":" TypeTerm
    ;

TypeTerm
    : TypeFactor {"*" TypeFactor}
    ;

TypeFactor
    : "(" [TypeTerm {"," TypeTerm}] ")" ["->" TypeTerm]
    | UpperID
    | LowerID
    ;
