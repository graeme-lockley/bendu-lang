uses "./Scanner.llld";

Program
    : {ImportStatement [";"]} {Declaration [";"]}
    ;

ImportStatement
    : "import" LiteralString ["as" UpperID] ["exposing" "(" ImportDeclaration {"," ImportDeclaration} ")"]
    ;

ImportDeclaration
    : LowerID ["as" LowerID]
    | UpperID ["as" UpperID]
    ;

Declaration
    : "type" TypeDeclaration {"and" TypeDeclaration}
    | Expression
    ;

TypeDeclaration
    : UpperID ["*" | "-"] [TypeParameters] "=" TypeConstructor {"|" TypeConstructor}
    ;

TypeConstructor
    : UpperID ["[" TypeTerm {"," TypeTerm} "]"]
    ;

Expression
    : "let" LetDeclaration {"and" LetDeclaration}
    | "print" "(" [Expression {"," Expression}] ")"
    | "println" "(" [Expression {"," Expression}] ")"
    | "abort" "(" [Expression {"," Expression}] ")"
    | "if" ["|"] Expression "->" Expression {"|" Expression ["->" Expression]}
    | "while" Expression "->" Expression
    | OrExpression
    ;

LetDeclaration
    : LowerID ["!"] ["*"] [TypeParameters] [FunctionParameters] [TypeQualifier] "=" Expression
    ;

OrExpression
    : Or
    ;

Or
    : AndE {"||" AndE}
    ;

AndE
    : Equality {"&&" Equality}
    ;

Equality
    : Starpend [RelOp Starpend]
    ;

RelOp
    : "=="
    | "!="
    | "<"
    | "<="
    | ">"
    | ">="
    ;

Starpend
    : Additive {StarpendOp Additive}
    ;

StarpendOp
    : ">>"
    | ">!"
    | "<<"
    | "<!"
    ;

Additive
    : Multiplicative {("+" | "-") Multiplicative}
    ;

Multiplicative
    : Power {("*" | "/" | "%") Power}
    ;

Power
    : TypedExpression {"**" TypedExpression}
    ;

TypedExpression
    : Assignment [":" TypeTerm]
    ;

Assignment
    : QualifiedExpression [":=" Expression]
    ;

QualifiedExpression
    : Factor {QualifiedExpressionSuffix}
    ;

QualifiedExpressionSuffix
    : "(" [Expression {"," Expression}] ")"
    | "!" (QualifiedExpression [":" [QualifiedExpression]] | ":" [QualifiedExpression])
    ;

Factor
    : "(" [Expression {"," Expression}] ")"
    | LowerID
    | UpperID ["." (LowerID | UpperID)]
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | "!" Factor
    | "@" Factor
    | "fn" [TypeParameters] FunctionParameters [TypeQualifier] ["="] Expression
    | "{" {Expression [";"]} "}"
    | "[" [["..."] Expression {"," ["..."] Expression}] "]"
    | "match" Expression ["|"] Case {Case}
    ;

FunctionParameters
    : "(" [FunctionParameter {"," FunctionParameter}] ")"
    ;

FunctionParameter
    : LowerID ["!"] [TypeQualifier]
    | "_" [TypeQualifier]
    | "(" FunctionParameter {"," FunctionParameter} ")" [TypeQualifier]
    ;

TypeParameters
    : "[" LowerID {"," LowerID} "]"
    ;

TypeQualifier
    : ":" TypeTerm
    ;

TypeTerm
    : TypeFactor {"*" TypeFactor}
    ;

TypeFactor
    : "(" [TypeTerm {"," TypeTerm}] ")" ["->" TypeTerm]
    | UpperID ["[" TypeTerm {"," TypeTerm} "]"]
    | LowerID
    ;

Case
    : Pattern ["," Expression] "->" Expression
    ;

Pattern
    : PatternFactor [TypeQualifier] ["@" LowerID]
    ;

PatternFactor
    : "(" [Pattern {"," Pattern}] ")"
    | LiteralChar
    | LiteralFloat
    | LiteralInt
    | LiteralString
    | "True"
    | "False"
    | LowerID
    | "_"
    | UpperID ["." UpperID] "(" [Pattern {"," Pattern}] ")"
    ;
