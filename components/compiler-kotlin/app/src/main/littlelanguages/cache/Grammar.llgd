uses "./Scanner.llld";

Program
    : [Declaration {";" Declaration}]
    ;

Declaration
    : "let" LowerID ["!"] ":" Scheme "=" LiteralInt
    | "fn" LowerID ["!"] ":" Scheme "=" LiteralInt [LiteralInt]
    | "type" UpperID [TypeParameters] ["=" TypeConstructor {"|" TypeConstructor}]
    ;

TypeConstructor
    : UpperID ["[" TypeTerm {"," TypeTerm} "]"] "=" LiteralInt
    ;

Scheme
    : [TypeParameters] TypeTerm
    ;

TypeParameters
    : "[" LowerID {"," LowerID} "]"
    ;

TypeTerm
    : TypeFactor {"*" TypeFactor}
    ;

TypeFactor
    : "(" [TypeTerm {"," TypeTerm}] ")" ["->" TypeTerm]
    | UpperID ["[" TypeTerm {"," TypeTerm} "]"]
    | LowerID
    ;
